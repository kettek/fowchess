behavior Tray
  on tray from GameUpdates
    set pieces to JSON.parse(event.data)
    for trayPiece in pieces
      make <div.Piece/> called piece
      set piece's @draggable to true
      set piece's @script to 'install Piece'
      add .{trayPiece.piece} to piece
      add .{trayPiece.color} to piece
      set piece's @data-piece to trayPiece.piece
      set piece's @data-color to trayPiece.color

      if the trayPiece's color is 'black' and my id is 'leftTray' then
        put piece at the end of me
      else
        if the trayPiece's color is 'white' and my id is 'rightTray' then
          put piece at the end of me
        end
      end
    end
  end

  on untake from GameUpdates
    set action to JSON.parse(event.data)
    set viablePiece to first .{action.piece+'.'+action.color} in me
    if viablePiece then
      put viablePiece into #{action.to.x+'x'+action.to.y}
    end
  end
end