behavior CreateDialog()
  init
    add .Dialog to me
    set dialogId to my @id

    make <header/> called header
    make <section/> called section
    make <footer/> called footer

    -- header
    put `<span>create a game</span>` into header
    make <button.Close/> called button
    put 'close' into button
    set button's @script to `on click remove #${dialogId}`
    put button at the end of header

    -- content
    put `
      <label>
        <span>name</span>
        <input id='playerName' name='playerName'/>
      </label>
      <label>
        <span>black</span>
        <input type='radio' name='playerColor' value='black' checked/>
      </label>
      <label>
        <span>white</span>
        <input type='radio' name='playerColor' value='white'/>
      </label>
    ` into section

    -- footer
    make <button.Submit/> called submitButton
    put 'create' into submitButton
    set submitButton's @script to `
      on click
        set color to the first <input[checked][name=playerColor]/>
        game.create({name: #playerName's value, color: color.value})
        remove #${dialogId}
      end
    `
    put submitButton at the end of footer

    put header at the end of me
    put section at the end of me
    put footer at the end of me
  end
end

behavior JoinDialog()
  init
    add .Dialog to me
    set dialogId to my @id

    make <header/> called header
    make <section/> called section
    make <footer/> called footer

    -- header
    put `<span>join a game</span>` into header
    make <button.Close/> called button
    put 'close' into button
    set button's @script to `on click remove #${dialogId}`
    put button at the end of header

    -- content
    put `
      <label>
        <span>id</span>
        <input type='text' id='joinId'/>
      </label>
    ` into section

    -- footer
    make <button.Submit/> called submitButton
    put 'join' into submitButton
    set submitButton's @script to `
      on click
        game.join(#joinId's value)
        remove #${dialogId}
      end
    `
    put submitButton at the end of footer

    put header at the end of me
    put section at the end of me
    put footer at the end of me

  end
end